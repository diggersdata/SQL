-- Load Data - DML

DECLARE @StartDate DATETIME = '01/01/2020' --Starting value of Date Range
DECLARE @EndDate DATETIME = '01/01/2025' --End Value of Date Range
DECLARE
	@DayOfWeekInMonth INT,
	@DayOfWeekInYear INT,
	@DayOfQuarter INT,
	@WeekOfMonth INT,
	@CurrentYear INT,
	@CurrentMonth INT,
	@CurrentQuarter INT

DECLARE @DayOfWeek TABLE (DOW INT, MonthCount INT, QuarterCount INT, YearCount INT)
INSERT INTO @DayOfWeek VALUES (1, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (2, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (3, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (4, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (5, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (6, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (7, 0, 0, 0)

DECLARE @CurrentDate AS DATETIME = @StartDate
SET @CurrentMonth = DATEPART(MM, @CurrentDate)
SET @CurrentYear = DATEPART(YY, @CurrentDate)
SET @CurrentQuarter = DATEPART(QQ, @CurrentDate)

WHILE @CurrentDate < @EndDate
BEGIN

IF @CurrentMonth != DATEPART(MM, @CurrentDate) 
	BEGIN
		UPDATE @DayOfWeek
		SET MonthCount = 0
		SET @CurrentMonth = DATEPART(MM, @CurrentDate)
	END

IF @CurrentQuarter != DATEPART(QQ, @CurrentDate)
	BEGIN
		UPDATE @DayOfWeek
		SET QuarterCount = 0
		SET @CurrentQuarter = DATEPART(QQ, @CurrentDate)
	END

IF @CurrentYear != DATEPART(YY, @CurrentDate)
	BEGIN
		UPDATE @DayOfWeek
		SET YearCount = 0
		SET @CurrentYear = DATEPART(YY, @CurrentDate)
	END

UPDATE @DayOfWeek
	SET 
		MonthCount = MonthCount + 1,
		QuarterCount = QuarterCount + 1,
		YearCount = YearCount + 1
	WHERE DOW = DATEPART(DW, @CurrentDate)

SELECT
		@DayOfWeekInMonth = MonthCount,
		@DayOfQuarter = QuarterCount,
		@DayOfWeekInYear = YearCount
	FROM @DayOfWeek
	WHERE DOW = DATEPART(DW, @CurrentDate)

INSERT INTO [edw].[DimDate]
	SELECT		
		CONVERT (char(8),@CurrentDate,112) as [DateKey],
		@CurrentDate AS [Date],
		DATEPART(DD, @CurrentDate) AS [DayOfMonth],
		DATENAME(DW, @CurrentDate) AS [DayName],
		@DayOfWeekInMonth AS [DayOfWeekInMonth],
		@DayOfWeekInYear AS [DayOfWeekInYear],
		@DayOfQuarter AS [DayOfQuarter],
		DATEPART(DY, @CurrentDate) AS [DayOfYear],
		DATEPART(WW, @CurrentDate) + 1 - DATEPART(WW, CONVERT(VARCHAR, DATEPART(MM, @CurrentDate)) + '/1/' + CONVERT(VARCHAR, DATEPART(YY, @CurrentDate))) AS [WeekOfMonth],
		(DATEDIFF(DD, DATEADD(QQ, DATEDIFF(QQ, 0, @CurrentDate), 0), @CurrentDate) / 7) + 1 AS [WeekOfQuarter],
		DATEPART(WW, @CurrentDate) AS [WeekOfYear],
		DATEPART(MM, @CurrentDate) AS [Month],
		DATENAME(MM, @CurrentDate) AS [MonthName],
		CASE
			WHEN DATEPART(MM, @CurrentDate) IN (1, 4, 7, 10) THEN 1
			WHEN DATEPART(MM, @CurrentDate) IN (2, 5, 8, 11) THEN 2
			WHEN DATEPART(MM, @CurrentDate) IN (3, 6, 9, 12) THEN 3
			END AS [MonthOfQuarter],
		DATEPART(QQ, @CurrentDate) AS [Quarter],
		CASE DATEPART(QQ, @CurrentDate)
			WHEN 1 THEN 'First'
			WHEN 2 THEN 'Second'
			WHEN 3 THEN 'Third'
			WHEN 4 THEN 'Fourth'
			END AS [QuarterName],
		DATEPART(YEAR, @CurrentDate) AS [Year],
		'CY ' + CONVERT(VARCHAR, DATEPART(YEAR, @CurrentDate)) AS [YearName],
		LEFT(DATENAME(MM, @CurrentDate), 3) + '-' + CONVERT(VARCHAR, DATEPART(YY, @CurrentDate)) AS [MonthYear],
		RIGHT('0' + CONVERT(VARCHAR, DATEPART(MM, @CurrentDate)),2) + CONVERT(VARCHAR, DATEPART(YY, @CurrentDate)) AS [MMYYYY],
		CONVERT(DATETIME, CONVERT(DATE, DATEADD(DD, - (DATEPART(DD,	@CurrentDate) - 1), @CurrentDate))) AS [FirstDayOfMonth],
		CONVERT(DATETIME, CONVERT(DATE, DATEADD(DD, - (DATEPART(DD,	(DATEADD(MM, 1, @CurrentDate)))), DATEADD(MM, 1, @CurrentDate)))) AS [LastDayOfMonth],
		DATEADD(QQ, DATEDIFF(QQ, 0, @CurrentDate), 0) AS [FirstDayOfQuarter],
		DATEADD(QQ, DATEDIFF(QQ, -1, @CurrentDate), -1) AS [LastDayOfQuarter],
		CONVERT(DATETIME, '01/01/' + CONVERT(VARCHAR, DATEPART(YY, @CurrentDate))) AS [FirstDayOfYear],
		CONVERT(DATETIME, '12/31/' + CONVERT(VARCHAR, DATEPART(YY, @CurrentDate))) AS [LastDayOfYear],
		CASE DATEPART(DW, @CurrentDate)
			WHEN 1 THEN 0
			WHEN 2 THEN 1
			WHEN 3 THEN 1
			WHEN 4 THEN 1
			WHEN 5 THEN 1
			WHEN 6 THEN 1
			WHEN 7 THEN 0
		END AS [IsWeekday]
	
	SET @CurrentDate = DATEADD(DD, 1, @CurrentDate)
END
